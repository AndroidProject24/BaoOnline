apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.fernandocejas.frodo'
android {
    compileSdkVersion rootProject.ext.compileVersion
    buildToolsVersion rootProject.ext.buildToolVersion
    defaultConfig {
        applicationId rootProject.ext.packageName
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName rootProject.ext.versionName
        versionCode rootProject.ext.versionCode
        multiDexEnabled true
    }
    signingConfigs {
        Key {
            try {
                storeFile file("toankey.iks")
                storePassword KEYSTORE_PASSWORD
                keyAlias "thekey"
                keyPassword KEY_PASSWORD
            }
            catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.Key
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
    android.applicationVariants.all { variant ->
        configApkFileName(variant, defaultConfig)
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
    }
    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res-main',
                    file('src/main/res-screen')
                            .listFiles()
                            .each { res.srcDirs += it.path }
            ]
            /*res.srcDirs = [
                    'src/main/res-main',
                    'src/main/res-screen/Home',
                    'src/main/res-screen/ReadNews',
            ]*/
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
@SuppressWarnings("GroovyAssignabilityCheck")
def configApkFileName(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (variant.buildType.name == android.buildTypes.debug.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, variant.buildType.name + "_v"
                        + defaultConfig.versionName + "_"
                        + defaultConfig.versionCode
                        + ".apk")
            }
        } else if (variant.buildType.name == android.buildTypes.release.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, "BaoOnline_" + variant.buildType.name
                        + "_v" + defaultConfig.versionName + "_"
                        + defaultConfig.versionCode
                        + ".apk")
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':library')
    //----- Dagger
    compile rootProject.ext.dagger
    apt rootProject.ext.daggerCompiler
    provided rootProject.ext.javaxannotation
    //-----Jsoup
    compile rootProject.ext.jsoup
    //----- Rx
    compile rootProject.ext.rxandroid
    compile rootProject.ext.rxjava
    compile rootProject.ext.rxbinding
    //-----TikXml
    compile rootProject.ext.tikxml
    compile rootProject.ext.tikxml_annotation
    compile rootProject.ext.tikxml_core
    apt rootProject.ext.tikxml_processor
}
