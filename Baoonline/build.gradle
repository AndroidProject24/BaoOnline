apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
//apply plugin: 'com.frogermcs.androiddevmetrics'
android {
    compileSdkVersion rootProject.ext.compileVersion
    buildToolsVersion rootProject.ext.buildToolVersion
    defaultConfig {
        applicationId rootProject.ext.packageName
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName rootProject.ext.versionName
        versionCode rootProject.ext.versionCode
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }
    android.applicationVariants.all { variant ->
        configApkFileName(variant, defaultConfig)
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
    }
    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res-main',
                    'src/main/res-screen/Home',
                    'src/main/res-screen/ReadNews',
            ]
        }
    }
}
@SuppressWarnings("GroovyAssignabilityCheck")
def configApkFileName(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (variant.buildType.name == android.buildTypes.debug.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, variant.buildType.name + "_v"
                        + defaultConfig.versionName + "_"
                        + defaultConfig.versionCode
                        + ".apk")
            }
        } else if (variant.buildType.name == android.buildTypes.release.name) {
            if (output.zipAlign) {
                def file = output.outputFile
                output.outputFile = new File(file.parentFile, "BaoOnline_" + variant.buildType.name
                        + "_v" + defaultConfig.versionName + "_"
                        + defaultConfig.versionCode
                        + ".apk")
            }
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //----- Support Libs
    compile rootProject.ext.supportAppcompat
    compile rootProject.ext.supportPercent
    compile rootProject.ext.supportDesign
    compile rootProject.ext.supportCardView
    compile rootProject.ext.supportRecyclerView
    compile rootProject.ext.guava
    //-----TikXml
    compile rootProject.ext.tikxml
    compile rootProject.ext.tikxml_annotation
    compile rootProject.ext.tikxml_core
    apt rootProject.ext.tikxml_processor
    //----- Dagger
    compile rootProject.ext.dagger
    apt rootProject.ext.daggerCompiler
    provided rootProject.ext.javaxannotation
    //----- Rx
    compile rootProject.ext.rxandroid
    compile rootProject.ext.rxjava
    compile rootProject.ext.rxlifecycle
    //----- Retrofit
    compile rootProject.ext.retrofitRxJavaAdapter
    compile rootProject.ext.okHttp
    compile rootProject.ext.okHttpLoggingInterceptor
    //----- Butterknife
    compile rootProject.ext.butterKnife
    apt rootProject.ext.apt_butterKnife
    //----- Glide
    compile rootProject.ext.glide
    compile rootProject.ext.fresco
    //-----Network
    compile rootProject.ext.reactivenetwork
    //-----Jsoup
    compile rootProject.ext.jsoup
    //----Logger
    compile rootProject.ext.logger
    //----- Testing
    //compile 'com.github.asLody:turbodex:1.0.0'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.github.moduth:blockcanary-android:1.2.1'
    compile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}
